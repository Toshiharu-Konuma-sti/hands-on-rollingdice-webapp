import com.github.spotbugs.snom.SpotBugsTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.3'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.cyclonedx.bom' version '2.3.1'
	id 'jacoco'
	id 'com.github.spotbugs' version '6.4.2'
	id 'pmd'
	id 'checkstyle'
	id 'org.springdoc.openapi-gradle-plugin' version '1.9.0'
	id 'org.openapi.generator' version '7.16.0'
	id 'maven-publish'
}

group = 'jp.sios'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
	all { repo ->
		if (repo instanceof MavenArtifactRepository && repo.url.toString().startsWith('http://')) {
			println "Allowing insecure protocol for repository: ${repo.url}"
			repo.allowInsecureProtocol = true
		}
	}
}

dependencies {

	// fundamental libraries necessary to run the environment of Spring Boot
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// libraries for Metrics in OpenTelemetry (using both OpenTelemetry and Prometheus protocol)
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	runtimeOnly 'io.micrometer:micrometer-registry-otlp'
	runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

	// library for Metrics to enable an exemplar
	implementation 'io.micrometer:micrometer-tracing-bridge-otel'

	// library for Logging and Tracing in OpenTelemetry
	implementation 'io.opentelemetry.instrumentation:opentelemetry-spring-boot-starter:2.12.0'

	// libraties necessary to connect to a database
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	runtimeOnly 'com.mysql:mysql-connector-j:9.2.0'

	// library for generating OpenAPI document
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'

}

tasks.named('test') {
	useJUnitPlatform()
	jvmArgs '-Djava.security.egd=file:/dev/./urandom'
	testLogging {
		events "started", "passed", "skipped", "failed"
	}
	exclude '**/OpenApiGeneratorTest.class'
}

jacoco {
	toolVersion = "0.8.13"
}

tasks.withType(JacocoReport) {
	reports {
		xml.required = true
		html.required = true
	}
}

spotbugs {
	ignoreFailures = true
}

tasks.withType(SpotBugsTask) {
	reports {
		xml.required = true
		html.required = true
	}
}

pmd {
	toolVersion = "7.17.0"
	ignoreFailures = true
	ruleSetFiles = files("${rootProject.projectDir}/config/pmd/pmd-ruleset.xml")
	sourceSets = [sourceSets.main, sourceSets.test]
}

tasks.withType(Pmd) {
	reports {
		xml.required = true
		html.required = true
	}
}

checkstyle {
	toolVersion = "10.26.1"
	configFile = file("${rootProject.projectDir}/config/checkstyle/google_checks.xml")
	configProperties = [
		'org.checkstyle.google.suppressionfilter.config': "${rootProject.projectDir}/config/checkstyle/checkstyle-suppressions.xml"
	]
}

openApi {
	apiDocsUrl = "http://localhost:8182/v3/api-docs"
	outputDir = file("$buildDir/docs/openapi")
}

openApiGenerate {
	generatorName = "html2"
	inputSpec.set("$buildDir/docs/openapi/openapi.json")
	outputDir.set("$buildDir/docs/swagger")
}

tasks.register('generateOpenApiDocsNoServer', Test) {
	description = 'Generates OpenAPI JSON using MockMvc test without starting the web server.'
	group = 'documentation'
	useJUnitPlatform()
	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath
	filter {
		includeTestsMatching "jp.sios.apisl.handson.rollingdice.webapp.webapi.OpenApiGeneratorTest"
	}
	outputs.upToDateWhen { false }
}

publishing {
	publications {
		maven(MavenPublication) {
			artifact bootJar
		}
	}
	repositories {
		maven {
			url "${System.getenv('CI_API_V4_URL')}/projects/${System.getenv('CI_PROJECT_ID')}/packages/maven"
			allowInsecureProtocol = true
			credentials(HttpHeaderCredentials) {
				name = 'Job-Token'
				value = System.getenv('CI_JOB_TOKEN')
			}
			authentication {
				header(HttpHeaderAuthentication)
			}
		}
	}
}
